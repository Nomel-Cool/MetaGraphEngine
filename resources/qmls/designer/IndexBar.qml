import QtQuick 2.15
import QtQuick.Controls 2.15
import QtQuick.Layouts 1.15
import CustomModules 1.0
/// <summary>
/// 用26个ComboBox存储A~Z开头的单词，并支持关键词搜索后高亮显示到置顶
/// </summary>
/// <property name="keyWord">用户可指定的搜索关键词</param>
///
/// <signal name="itemSelected">
/// <param name="selectedItem">选中的关键词</param>
/// </signal>
///
/// <function name="pin2TopInListView">
/// <summary>将关键词关联的ComboBox置顶</summary>
/// <param name="list_view">ComboBox所在的ListView的id</param>
/// <param name="indice">ListView中ComboBox的索引</param>
/// </function>
///
/// <function name="openComboBoxInListView">
/// <summary>将ListView中被选中的ComboBox展开</summary>
/// <param name="list_view">ComboBox所在的ListView的id</param>
/// <param name="indice">ListView中ComboBox的索引</param>
/// </function>
///
/// <function name="focusItemInComboBoxByKeyword">
/// <summary>若关键词命中，则高亮展开的ComboBox里的项</summary>
/// <param name="list_view">ComboBox所在的ListView的id</param>
/// <param name="indice">ListView中ComboBox的索引</param>
/// </function>
///
/// <function name="addWord">
/// <summary>前端添加模型名称到图元栏的接口</summary>
/// <param name="word">所添加的模型名称</param>
/// </function>

Item {
    property string keyWord: ""
    signal previewItemSelected(string selectedItem)
    signal drawItemSelected(string selectedItem)

    QIndexBarFactory {
        id: indexBarFactoryImpl
    }

    ListModel {
        id: wordModel
    }

    Component.onCompleted: {
        for (var i = 0; i < 26; i++)
            wordModel.append({ letter: String.fromCharCode(65 + i), words: [ {showText : String.fromCharCode(65 + i) + "Hello"}] });
        var modelNameList = indexBarFactoryImpl.Request4ModelName();
        for (var j = 0; j < modelNameList.length; ++j) { 
            var modelName = modelNameList[j]; 
            var firstLetter = modelName.charAt(0).toUpperCase(); 
            var index = firstLetter.charCodeAt(0) - 65; 
            if (index >= 0 && index < 26)
                wordModel.get(index).words.append({ showText: modelName });
            // 如果需要动态更新视图，似乎需要对model用到它的set方法强制更新到视图
        }
    }

    ListView {
        id: listView
        width: parent.width
        height: parent.height
        spacing: 0
        clip: true
        model: wordModel

        delegate: ComboBox {
            id: comboBox
            width: listView.width
            height: Math.max(40, implicitHeight) // 确保高度不小于隐含高度
            visible: keyWord.length === 0 ? true : letter === keyWord.toUpperCase()[0]
            displayText: letter
            model: words
            clip: true
            delegate: Rectangle {
                width: comboBox.width
                height: comboBox.height // 设置项的高度等于 ComboBox 的高度
                implicitHeight: 40
                color: comboBox.currentIndex === index ? "lightBlue" : "white" // Highlight text color for the selected item
                Text {
                    anchors.fill: parent
                    verticalAlignment: Text.AlignVCenter
                    horizontalAlignment: Text.AlignHCenter
                    text: model.showText // showText与添加的json结构的键值对一一对应
                }
                MouseArea { 
                    anchors.fill: parent
                    acceptedButtons: Qt.LeftButton|Qt.RightButton
                    onClicked: function(mouse) { 
                        if (mouse.button === Qt.RightButton) { 
                            comboBox.currentIndex = index; 
                            previewItemSelected(currentText);
                        } 
                        else if (mouse.button === Qt.LeftButton) { 
                            drawItemSelected(currentText);
                        } 
                    }
                }
            }
        }
    }

    // React to changes in the keyWord property
    onKeyWordChanged: {
        console.log("KeyWord changed to:", keyWord);

        if (keyWord.length === 0) {
            console.log("Empty keyWord, no visibility changes.");
            return;
        }

        var firstLetter = keyWord[0].toUpperCase();
        if (firstLetter.charCodeAt(0) < 65 || firstLetter.charCodeAt(0) > 90) {
            console.log("Invalid keyWord, no visibility changes.");
            return;
        }

        // Find the index of the letter in the ListModel
        for (var i = 0; i < wordModel.count; i++) {
            if (wordModel.get(i).letter === firstLetter) {
                pin2TopInListView(listView, i);
                openComboBoxInListView(listView, i);
                focusItemInComboBoxByKeyword(listView, i);
                break;
            }
        }
    }
    function pin2TopInListView(list_view, indice) {
        list_view.currentIndex = indice; // Move the corresponding ComboBox to the top
        list_view.positionViewAtIndex(indice, ListView.Beginning); // Ensure it's at the top of the view
    }
    function openComboBoxInListView(list_view, indice) {
        listView.itemAtIndex(indice).popup.open()
    }
    function focusItemInComboBoxByKeyword(list_view, indice) {
        for (var j = 0; j < list_view.itemAtIndex(indice).count; j++) {
            var searchText = list_view.itemAtIndex(indice).textAt(j);
            if (searchText === keyWord) {
                list_view.itemAtIndex(indice).currentIndex = j; 
                break;
            }
        }
    }
    function addWord(word) {
        if (!word || word.length === 0) {
            console.log("Empty word entered");
            return;
        }

        var firstLetter = word[0].toUpperCase();
        var index = firstLetter.charCodeAt(0) - 65;

        if (index < 0 || index > 25) {
            console.log("Invalid word entered");
            return;
        }

        // Get the data item corresponding to the letter
        var dataItem = wordModel.get(index);

        if (!dataItem.words) {
            console.error("words is undefined for letter", dataItem.letter);
            return;
        }

        // Check for duplicates
        for (var i = 0; i < dataItem.words.count; i++) {
            if (dataItem.words.get(i).showText === word) {
                console.log("Duplicating word entered");
                return;
            }
        }

        // Add the new word
        dataItem.words.append({ showText: word });
        console.log("Updated words for letter", dataItem.letter, ":", dataItem.words);
    }
function delItem(selectedItem) {
    if (!selectedItem || selectedItem.length === 0) {
        console.log("Empty selectedItem");
        return;
    }

    var firstLetter = selectedItem[0].toUpperCase();
    var index = firstLetter.charCodeAt(0) - 65;

    if (index < 0 || index > 25) {
        console.log("Invalid selectedItem");
        return;
    }

    // Get the data item corresponding to the letter
    var dataItem = wordModel.get(index);

    if (!dataItem.words) {
        console.error("words is undefined for letter", dataItem.letter);
        return;
    }

    // Check for duplicates
    for (var i = 0; i < dataItem.words.count; i++) {
        if (dataItem.words.get(i).showText === selectedItem) {
            dataItem.words.remove(i, 1);
            console.log("Deleted item:", selectedItem);
            break;
        }
    }

    // 强制更新布局
    listView.forceLayout();
}

}
